plugins {
  id 'org.openjfx.javafxplugin' version '0.0.8' apply false
}

if (JavaVersion.current().isJava11Compatible()) {
  apply plugin: 'org.openjfx.javafxplugin'

  javafx {
    version = "11"
    modules = ['javafx.controls', 'javafx.fxml']
  }
}

dependencies {
  compile project(':lib')
}

sourceSets.main {
  java.srcDir 'src'
  resources {
    srcDir 'src'
    include '**/*.png'
    include '**/*.fxml'
  }
}

jar {
  archiveName = 'ChunkyLauncher.jar'
  manifest.attributes 'Main-Class': 'se.llbit.chunky.launcher.ChunkyLauncher'

  // Include classes from the common library.
  from project(':lib').configurations.archives.allArtifacts.files.collect {
    zipTree(it)
  }
  from configurations.jsonlib.files.collect {
    zipTree(it)
  }
}
