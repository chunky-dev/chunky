apply plugin: 'application'

mainClassName = 'se.llbit.chunky.launcher.ChunkyLauncher'

configurations {
    bundled
    implementation.extendsFrom configurations.bundled
    implementation.extendsFrom configurations.jsonlib
}

dependencies {
    bundled 'com.vdurmont:semver4j:3.1.0'

    implementation project(':lib')
}

sourceSets.main {
    java.srcDir 'src'
    resources {
        srcDir 'src'
        include '**/*.png'
        include '**/*.fxml'
    }
}

jar {
    manifest.attributes 'Main-Class': mainClassName

    // Include classes from the common library.
    from project(':lib').configurations.archives.allArtifacts.files.collect {
        zipTree(it)
    }
    from configurations.jsonlib.files.collect {
        zipTree(it)
    }

    // Include other dependencies
    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
